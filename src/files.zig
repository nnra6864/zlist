const std = @import("std");
const mem = std.mem;
const Terminal = std.Io.Terminal;
const testing = std.testing;

const file = @import("file.zig");
const opts = @import("opts.zig");

pub const Files = struct {
    const Self = @This();

    allocator: mem.Allocator,
    io: std.Io,
    items: std.ArrayList(file.File),
    opt: opts.FilesOptions,

    icon_inventory: std.StaticStringMap([]const u8) = std.StaticStringMap([]const u8).initComptime(.{
        .{ ".zig", " " },
        .{ ".go", " " },
        .{ ".rs", " " },
        .{ ".c", " " },
        .{ ".cpp", " " },
        .{ ".h", " " },
        .{ ".js", " " },
        .{ ".ts", " " },
        .{ ".py", " " },
        .{ ".java", " " },
        .{ ".md", " " },
        .{ ".txt", " " },
        .{ ".png", " " },
        .{ ".jpg", " " },
        .{ ".jpeg", " " },
        .{ ".gif", " " },
        // default file icon
        .{ "", " " },
    }),

    color_inventory: std.StaticStringMap(Terminal.Color) = std.StaticStringMap(Terminal.Color).initComptime(.{
        .{ ".md", Terminal.Color.bright_magenta },
        .{ ".png", Terminal.Color.bright_cyan },
        .{ ".jpg", Terminal.Color.bright_cyan },
        .{ ".jpeg", Terminal.Color.bright_cyan },
        .{ ".gif", Terminal.Color.bright_cyan },
        .{ "", Terminal.Color.bright_yellow },
    }),

    /// init a Files from a directory
    pub fn init(
        allocator: mem.Allocator,
        io: std.Io,
        dir: std.Io.Dir,
        opt: opts.FilesOptions,
    ) !Self {
        var files = try std.ArrayList(file.File).initCapacity(allocator, 32);
        errdefer files.deinit(allocator);

        var it = dir.iterate();
        while (try it.next(io)) |entry| {
            var fs = (try file.File.init(io, &entry, &dir, .{ .show_detail = opt.show_detail, .show_hidden = opt.show_hidden })) orelse continue;

            // copy name
            var name: []const u8 = undefined;
            name = try allocator.dupe(u8, entry.name);
            fs.name = name;

            try files.append(allocator, fs);
        }

        switch (opt.sort_type) {
            .length => {
                // sort by name length
                mem.sortUnstable(file.File, files.items, {}, file.File.nameLenLessThan);
            },
            else => {
                // sort by name ascending
                mem.sortUnstable(file.File, files.items, {}, file.File.nameLessThan);
            },
        }

        return .{
            .allocator = allocator,
            .io = io,
            .items = files,
            .opt = opt,
        };
    }

    pub fn deinit(self: *Self) void {
        for (self.items.items) |item| {
            self.allocator.free(item.name);
        }

        self.items.deinit(self.allocator);
    }

    /// list files in simple mode
    pub fn list(self: Self, comptime pure: bool) !void {
        // stdout
        var stdout_buf: [4096]u8 = undefined;
        const stdout_file = std.Io.File.stdout();
        var stdout_writer = stdout_file.writer(self.io, &stdout_buf);

        const stdout = &stdout_writer.interface;
        // get term
        const term = try self.getTerminal(stdout, stdout_file);

        const max_display_len = self.getMaxDisplayLen();
        const term_width = self.getTerminalWidth(stdout_file.handle);
        const col_width = max_display_len + 2; // 2 spaces padding
        var cols = term_width / col_width;
        if (cols < 1) {
            cols = 1;
        }

        for (self.items.items, 0..) |val, i| {
            const icon = self.getIcon(val.is_dir, val.name);

            if (!pure) {
                // set color
                try term.setColor(self.getColor(val.is_dir, val.name));
                // print item
                try term.writer.print(comptime opts.PrintMode.Normal.toString(), .{
                    icon,
                    val.name,
                    max_display_len - icon.len + 1, // +1 for padding
                });
            } else {
                // pure mode
                try term.writer.print(comptime opts.PrintMode.NormalPure.toString(), .{ val.name, max_display_len + 1 });
            }

            if (!pure) {
                // reset color
                try term.setColor(Terminal.Color.reset);
            }

            // make sure to print newline after each row
            if ((i + 1) % cols == 0) {
                try term.writer.print("\n", .{});
            }
        }

        try term.writer.print("\n", .{});
        try term.writer.flush();
    }

    /// get terminal width
    inline fn getTerminalWidth(_: Self, handle: std.Io.File.Handle) usize {
        var winsize = std.mem.zeroes(std.posix.winsize);
        if (std.c.ioctl(handle, std.c.T.IOCGWINSZ, @intFromPtr(&winsize)) == 0) {
            return winsize.col;
        }

        // default width
        return 80;
    }

    /// get max display length of file names, including icons
    inline fn getMaxDisplayLen(self: Self) usize {
        var max_len: usize = 0;
        for (self.items.items) |val| {
            const curr_len = val.name.len + self.getIcon(val.is_dir, val.name).len;

            if (curr_len > max_len) {
                max_len = curr_len;
            }
        }

        return max_len;
    }

    inline fn getIcon(self: Self, is_dir: bool, name: []const u8) []const u8 {
        if (is_dir) {
            return " ";
        }

        const ext = std.fs.path.extension(name);
        if (self.icon_inventory.get(ext)) |icon| {
            return icon;
        }

        // return default icons based on extension
        return " ";
    }

    inline fn getColor(self: Self, is_dir: bool, name: []const u8) Terminal.Color {
        if (is_dir) {
            return Terminal.Color.bright_blue;
        }

        const ext = std.fs.path.extension(name);
        if (self.color_inventory.get(ext)) |color| {
            return color;
        }

        // default file color
        return Terminal.Color.bright_yellow;
    }

    /// list files in detail mode
    pub fn listDetail(self: Self, comptime pure: bool) !void {
        // stdout
        var stdout_buf: [4096]u8 = undefined;
        const stdout_file = std.Io.File.stdout();
        var stdout_writer = stdout_file.writer(self.io, &stdout_buf);

        const stdout = &stdout_writer.interface;
        // get term
        const term = try self.getTerminal(stdout, stdout_file);

        var perm_buf: [10]u8 = undefined;
        var size_buf: [32]u8 = undefined;
        var time_buf: [32]u8 = undefined;

        for (self.items.items) |val| {
            if (!pure) {
                // first, set color
                try term.setColor(self.getColor(val.is_dir, val.name));

                try term.writer.print(comptime opts.PrintMode.Detail.toString(), .{
                    val.getPermissions(&perm_buf),
                    val.username,
                    val.groupname,
                    try val.humanSize(&size_buf),
                    try val.formatTime(&time_buf),
                    self.getIcon(val.is_dir, val.name),
                    val.name,
                });
            } else {
                // pure mode, no color and no icon
                try term.writer.print(comptime opts.PrintMode.DetailPure.toString(), .{
                    val.getPermissions(&perm_buf),
                    val.username,
                    val.groupname,
                    try val.humanSize(&size_buf),
                    try val.formatTime(&time_buf),
                    val.name,
                });
            }

            if (!pure) {
                // reset color
                try term.setColor(Terminal.Color.reset);
            }
            try term.writer.print("\n", .{});
        }

        try term.writer.flush();
    }

    /// list files recursively
    pub fn listRecursive(
        self: Self,
        term: Terminal,
        prefix: []const u8,
        first: bool,
        dir: std.Io.Dir,
        comptime pure: bool,
    ) !void {
        if (first) {
            try term.writer.print(".\n", .{});
        }

        const total = self.items.items.len;

        for (self.items.items, 0..) |val, i| {
            const is_last = (i == total - 1);
            const connector = if (is_last) "└──" else "├──";

            // print prefix and connector
            if (!pure) {
                // set color for prefix and connector
                try term.setColor(Terminal.Color.bright_blue);
            }
            try term.writer.print(comptime opts.PrintMode.RecursivePrefix.toString(), .{
                prefix,
                connector,
            });
            if (!pure) {
                // reset color
                try term.setColor(Terminal.Color.reset);
            }

            // print file/directory name
            if (!pure) {
                try term.setColor(self.getColor(val.is_dir, val.name));

                try term.writer.print(comptime opts.PrintMode.RecursiveWithFileMeta.toString(), .{
                    self.getIcon(val.is_dir, val.name),
                    val.name,
                });
            } else {
                // pure mode, no color and no icon
                try term.writer.print(comptime opts.PrintMode.RecursiveWithFileMetaPure.toString(), .{val.name});
            }
            if (!pure) {
                try term.setColor(Terminal.Color.reset);
            }

            if (val.is_dir) {
                const sub_dir = try dir.openDir(self.io, val.name, .{ .iterate = true });
                defer sub_dir.close(self.io);

                var sub_arena = std.heap.ArenaAllocator.init(self.allocator);
                defer sub_arena.deinit();

                var sub_files = try Files.init(
                    sub_arena.allocator(),
                    self.io,
                    sub_dir,
                    self.opt,
                );
                defer sub_files.deinit();

                // recursive itself
                const child_connector = if (is_last) "    " else "│   ";

                var buf: [128]u8 = undefined;
                const new_prefix = try std.fmt.bufPrint(&buf, "{s}{s}", .{ prefix, child_connector });

                try sub_files.listRecursive(term, new_prefix, false, sub_dir, pure);
            }
        }
    }

    /// get terminal info
    pub inline fn getTerminal(self: Self, writer: anytype, f: std.Io.File) !Terminal {
        const term_mode = try Terminal.Mode.detect(self.io, f, false, false);
        return Terminal{
            .mode = term_mode,
            .writer = writer,
        };
    }
};

test "get_detail" {
    const io = testing.io;

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test1.txt", .data = "hello 1" });
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test2.txt", .data = "hello 2" });

    try tmp_dir.dir.createDirPath(io, "sub_dir");

    const allocator = testing.allocator;

    var files = try Files.init(
        allocator,
        io,
        tmp_dir.dir,
        .{ .show_detail = true },
    );
    defer files.deinit();

    try files.listDetail(false);
}

test "recursive" {
    const io = testing.io;

    var tmp_dir = testing.tmpDir(.{});
    defer tmp_dir.cleanup();
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test1.txt", .data = "hello 1" });
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test2.txt", .data = "hello 2" });

    try tmp_dir.dir.createDirPath(io, "sub_dir");
    var tmp_sub_dir = try tmp_dir.dir.openDir(io, "sub_dir", .{ .iterate = true });
    defer tmp_sub_dir.close(io);
    _ = try tmp_sub_dir.createFile(io, "sub_test1.txt", .{});

    const allocator = testing.allocator;

    // stdout
    var stdout_buf: [4096]u8 = undefined;
    const stdout_file = std.Io.File.stdout();
    var stdout_writer = stdout_file.writer(io, &stdout_buf);

    var files = try Files.init(
        allocator,
        io,
        tmp_dir.dir,
        .{},
    );
    defer files.deinit();

    const term = try files.getTerminal(&stdout_writer.interface, stdout_file);

    try files.listRecursive(term, "", true, tmp_dir.dir, false);
    try stdout_writer.interface.flush();
}
