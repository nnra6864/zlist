const std = @import("std");
const Terminal = std.Io.Terminal;

const zlist = @import("zlist");

/// The render options that are determined at compile time.
const ModeOptionsComptime = struct {
    const Self = @This();
    pure: bool = false,

    pub inline fn initPure() Self {
        return comptime Self{
            .pure = true,
        };
    }
};

pub const LongViewOptions = struct {
    header: bool = false,
    show_permissions: bool = true,
    show_user: bool = true,
    show_group: bool = true,
    show_size: bool = true,
    show_time: bool = true,
    show_icon: bool = true,
};

pub const RootDisplay = enum {
    /// displays a dot
    dot,
    /// displays the root dir name
    name,
    /// root dir is not displayed and first level is not indented
    none,
};

pub const ColorUse = enum {
    auto,
    always,
    never,
};

const PrintMode = enum {
    Detail,
    DetailPure,
    DetailWithGit,
    RecursivePrefix,
    RecursiveWithFileMeta,
    RecursiveWithFileMetaPure,

    pub inline fn toString(self: PrintMode) []const u8 {
        return switch (self) {
            .Detail => "  {s:<11} {s:<8} {s:<8} {s:<8} {s:<8}  {s} {s}",
            .DetailPure => "  {s:<11} {s:<8} {s:<8} {s:<8} {s:<8}  {s}",
            .DetailWithGit => "    {c} {s:<11} {s:<8} {s:<8} {s:<8} {s:<8}  {s} {s}",
            .RecursivePrefix => "{s}{s}",
            .RecursiveWithFileMeta => " {s}{s}\n",
            .RecursiveWithFileMetaPure => " {s}\n",
        };
    }
};

const icon_inventory = std.StaticStringMap([]const u8).initComptime(.{
    .{ ".zig", "፠ " },
    .{ ".zon", "፠ " },
    .{ ".go", "፠ " },
    .{ ".rs", "፠ " },
    .{ ".c", "፠ " },
    .{ ".cpp", "፠ " },
    .{ ".h", "፠ " },
    .{ ".js", "፠ " },
    .{ ".ts", "፠ " },
    .{ ".py", "፠ " },
    .{ ".java", "፠ " },
    .{ ".md", "፠ " },
    .{ ".txt", "፠ " },
    .{ ".png", "፠ " },
    .{ ".jpg", "፠ " },
    .{ ".jpeg", "፠ " },
    .{ ".gif", "፠ " },
    .{ ".sh", "፠ " },
    // default file icon
    .{ "", "፠ " },
});

const color_inventory = std.StaticStringMap(Terminal.Color).initComptime(.{
    .{ ".md", Terminal.Color.bright_cyan },
    .{ ".png", Terminal.Color.bright_cyan },
    .{ ".jpg", Terminal.Color.bright_cyan },
    .{ ".jpeg", Terminal.Color.bright_cyan },
    .{ ".gif", Terminal.Color.bright_cyan },
    .{ ".sh", Terminal.Color.bright_cyan },
    .{ "", Terminal.Color.bright_cyan },
});

/// list files in simple mode
pub fn list(
    files: zlist.Files,
    term: Terminal,
    handle: std.Io.File.Handle,
    comptime mode_opt: ModeOptionsComptime,
) !void {
    const term_width = getTerminalWidth(handle);
    const entries = files.entries();
    const total_items = entries.len;

    if (total_items == 0) {
        return;
    }

    // Max possible columns (assuming each item is at least 1 char + padding)
    const MAX_COLS: usize = 512;
    var max_possible_cols = term_width / 3;
    if (max_possible_cols == 0) {
        max_possible_cols = 1;
    }
    if (max_possible_cols > MAX_COLS) {
        max_possible_cols = MAX_COLS;
    }
    if (max_possible_cols > total_items) {
        max_possible_cols = total_items;
    }

    var optimal_cols: usize = 1;
    var optimal_rows: usize = total_items;
    var col_widths: [MAX_COLS]usize = undefined;
    var final_col_widths: [MAX_COLS]usize = undefined;

    var current_cols = max_possible_cols;
    while (current_cols > 1) : (current_cols -= 1) {
        // rows = cell(total_items / cols)
        const rows = (total_items + current_cols - 1) / current_cols;
        @memset(col_widths[0..current_cols], 0);

        var total_width: usize = 0;
        var valid = true;

        for (0..current_cols) |c| {
            for (0..rows) |r| {
                const idx = c * rows + r;
                if (idx >= total_items) continue;

                const item = entries[idx];
                // visual length: pure mode has 2 space prefix. normal mode has 2 space + icon(2) = 4.
                const item_len = if (mode_opt.pure) item.name.len + 2 else item.name.len + 4;

                if (item_len > col_widths[c]) {
                    col_widths[c] = item_len;
                }
            }
            total_width += col_widths[c];
            if (c < current_cols - 1) {
                total_width += 1; // 1 space between columns
            }

            if (total_width > term_width) {
                valid = false;
                break;
            }
        }

        if (valid) {
            optimal_cols = current_cols;
            optimal_rows = rows;
            @memcpy(final_col_widths[0..current_cols], col_widths[0..current_cols]);
            break;
        }
    }

    if (optimal_cols == 1) {
        optimal_rows = total_items;
        final_col_widths[0] = files.maxDisplayLen() + (if (mode_opt.pure) 2 else 4);
    }

    for (0..optimal_rows) |r| {
        for (0..optimal_cols) |c| {
            const idx = c * optimal_rows + r;
            if (idx >= total_items) continue;

            const val = entries[idx];
            const item_len = if (mode_opt.pure) val.name.len + 2 else val.name.len + 4;

            // Print prefix, icon and name
            if (!mode_opt.pure) {
                const icon = getIcon(val.is_dir, val.name);
                try term.writer.print("  ", .{});

                try term.setColor(getColor(val.is_dir, val.name));
                try term.writer.print("{s}{s}", .{ icon, val.name });
                try term.setColor(Terminal.Color.reset);
            } else {
                try term.writer.print("  {s}", .{val.name});
            }

            // Print padding
            if (c < optimal_cols - 1) {
                const padding = final_col_widths[c] - item_len + 1; // +1 for inter-column space
                for (0..padding) |_| {
                    try term.writer.print(" ", .{});
                }
            }
        }
        try term.writer.print("\n", .{});
    }
}

/// list files in detail mode
pub fn listDetail(
    files: zlist.Files,
    term: Terminal,
    comptime mode_opt: ModeOptionsComptime,
    view_opt: LongViewOptions,
) !void {
    var perm_buf: [10]u8 = undefined;
    var size_buf: [32]u8 = undefined;
    var time_buf: [32]u8 = undefined;
    var display_name_buf: [std.fs.max_path_bytes]u8 = undefined;

    const show_git = files.hasGitStatus() and !mode_opt.pure;

    // calculate column widths
    const git_len: usize = if (view_opt.header) 3 else 1;
    var user_len: usize = if (view_opt.header) 4 else 0;
    var group_len: usize = if (view_opt.header) 5 else 0;
    var size_len: usize = 4;
    var time_len: usize = if (view_opt.header) 4 else 0;

    for (files.entries()) |val| {
        if (view_opt.show_user) user_len = @max(user_len, val.username.len);
        if (view_opt.show_group) group_len = @max(group_len, val.groupname.len);
        if (view_opt.show_size) size_len = @max(size_len, (try val.humanSize(&size_buf)).len);
        if (view_opt.show_time) time_len = @max(time_len, (try val.formatTime(&time_buf)).len);
    }

    if (view_opt.header) {
        if (show_git) try term.writer.print("Git ", .{});
        if (view_opt.show_permissions) try term.writer.print("Permissions ", .{});
        if (view_opt.show_user) try term.writer.print("{s:<[1]} ", .{ "User", user_len });
        if (view_opt.show_group) try term.writer.print("{s:<[1]} ", .{ "Group", group_len });
        if (view_opt.show_size) try term.writer.print("{s:>[1]} ", .{ "Size", size_len });
        if (view_opt.show_time) try term.writer.print("{s:<[1]} ", .{ "Time", time_len });
        try term.writer.print("Name\n", .{});
    }

    for (files.entries()) |val| {
        if (!mode_opt.pure) {
            try term.setColor(getColor(val.is_dir, val.name));
        }

        if (show_git) {
            const git_char = getGitStatusChar(files, val.name) orelse ' ';
            const git_color = getGitStatusColor(files, val.name);
            try term.setColor(git_color);
            try term.writer.print("{c:>[1]} ", .{ git_char, git_len });
        }

        if (view_opt.show_permissions) try term.writer.print("{s:<11} ", .{val.getPermissions(&perm_buf)});
        if (view_opt.show_user) try term.writer.print("{s:<[1]} ", .{ val.username, user_len });
        if (view_opt.show_group) try term.writer.print("{s:<[1]} ", .{ val.groupname, group_len });
        if (view_opt.show_size) try term.writer.print("{s:>[1]} ", .{ try val.humanSize(&size_buf), size_len });
        if (view_opt.show_time) try term.writer.print("{s:>[1]} ", .{ try val.formatTime(&time_buf), time_len });
        if (view_opt.show_icon and !mode_opt.pure) try term.writer.print("{s}", .{getIcon(val.is_dir, val.name)});
        try term.writer.print("{s}", .{try val.formatLongDisplayName(&display_name_buf)});

        if (!mode_opt.pure) {
            try term.setColor(Terminal.Color.reset);
        }
        try term.writer.print("\n", .{});
    }
}

/// list files recursively
pub fn listRecursive(
    root_dir: []const u8,
    files: *zlist.Files,
    term: Terminal,
    prefix: []const u8,
    first: bool,
    dir: std.Io.Dir,
    comptime mode_opt: ModeOptionsComptime,
    root_display: RootDisplay,
) !void {
    if (first) {
        switch (root_display) {
            .dot => try term.writer.print(".\n", .{}),
            .name => {
                try term.setColor(getColor(true, root_dir));
                try term.writer.print("{s}{s}\n", .{ getIcon(true, root_dir), root_dir });
                try term.setColor(Terminal.Color.reset);
            },
            .none => {},
        }
    }

    if (files.recursionLimitReached()) {
        return;
    }

    const entries = files.entries();
    const total = entries.len;

    for (entries, 0..) |val, i| {
        const is_last = (i == total - 1);
        const connector = if (first and root_display == .none) "" else if (is_last) "└──" else "├──";

        // print prefix and connector
        if (!mode_opt.pure) {
            // set color for prefix and connector
            try term.setColor(Terminal.Color.bright_blue);
        }
        try term.writer.print(comptime PrintMode.RecursivePrefix.toString(), .{
            prefix,
            connector,
        });
        if (!mode_opt.pure) {
            // reset color
            try term.setColor(Terminal.Color.reset);
        }

        // print file/directory name
        if (!mode_opt.pure) {
            try term.setColor(getColor(val.is_dir, val.name));

            try term.writer.print(comptime PrintMode.RecursiveWithFileMeta.toString(), .{
                getIcon(val.is_dir, val.name),
                val.name,
            });
        } else {
            // pure mode, no color and no icon
            try term.writer.print(comptime PrintMode.RecursiveWithFileMetaPure.toString(), .{val.name});
        }
        if (!mode_opt.pure) {
            try term.setColor(Terminal.Color.reset);
        }

        if (val.is_dir) {
            const sub_dir = dir.openDir(files.io, val.name, .{ .iterate = true }) catch |err| {
                // print error message and continue
                try term.writer.print("\x1b[31mzl: cannot open directory '{s}': {any}\x1b[0m\n", .{ val.name, err });
                continue;
            };
            defer sub_dir.close(files.io);

            var sub_arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
            defer sub_arena.deinit();

            var sub_files = try zlist.Files.init(
                sub_arena.allocator(),
                files.io,
                sub_dir,
                files.options(),
            );
            sub_files.setRecursionLevel(files.nextRecursionLevel());

            // recursive itself
            const child_connector = if (first and root_display == .none) " " else if (is_last) "    " else "│   ";

            // concat prefix and child connector for subdirectory
            var prefix_builder = try std.ArrayList(u8).initCapacity(files.allocator, prefix.len + child_connector.len);
            defer prefix_builder.deinit(files.allocator);

            try prefix_builder.appendSlice(files.allocator, prefix);
            try prefix_builder.appendSlice(files.allocator, child_connector);

            try listRecursive(root_dir, &sub_files, term, prefix_builder.items, false, sub_dir, mode_opt, root_display);

            // accumulate counts from subdirectories
            files.addReportTotals(sub_files);
        }
    }
}

/// print report after listing.
/// it will not flush the writer, plz flush it by yourself after calling this function.
pub inline fn printReport(files: zlist.Files, writer: anytype) !void {
    try writer.print(
        "\n  Found {d} contents in directory.\n  Folders: {d}\n  Files: {d}\n",
        .{
            files.total_folders + files.total_files,
            files.total_folders,
            files.total_files,
        },
    );
}

/// get terminal info
pub inline fn getTerminal(io: std.Io, writer: anytype, f: std.Io.File, color_use: ColorUse) !Terminal {
    const no_color = if (color_use == .never)
        true
    else if (color_use == .always)
        false
    else if (std.c.getenv("NO_COLOR")) |val|
        !std.mem.eql(u8, std.mem.span(val), "0")
    else
        false;

    const force_color = if (color_use == .always)
        true
    else if (color_use == .never)
        false
    else if (std.c.getenv("CLICOLOR_FORCE")) |val|
        !std.mem.eql(u8, std.mem.span(val), "0")
    else
        false;

    const term_mode = try Terminal.Mode.detect(io, f, no_color, force_color);
    return Terminal{
        .mode = term_mode,
        .writer = writer,
    };
}

/// get terminal width
inline fn getTerminalWidth(handle: std.Io.File.Handle) usize {
    var winsize = std.mem.zeroes(std.posix.winsize);
    if (std.c.ioctl(handle, std.c.T.IOCGWINSZ, @intFromPtr(&winsize)) == 0) {
        return winsize.col;
    }

    // default width
    return 80;
}

inline fn getIcon(is_dir: bool, name: []const u8) []const u8 {
    if (is_dir) {
        return "፨ ";
    }

    const ext = std.fs.path.extension(name);
    if (icon_inventory.get(ext)) |icon| {
        return icon;
    }

    // return default icons based on extension
    return "፠ ";
}

inline fn getColor(is_dir: bool, name: []const u8) Terminal.Color {
    if (is_dir) {
        return Terminal.Color.bright_blue;
    }

    const ext = std.fs.path.extension(name);
    if (color_inventory.get(ext)) |color| {
        return color;
    }

    // default file color
    return Terminal.Color.bright_cyan;
}

inline fn getGitStatusChar(files: zlist.Files, name: []const u8) ?u8 {
    const status = files.gitStatus(name) orelse return null;
    return switch (status) {
        .modified => 'M',
        .added => 'A',
        .deleted => 'D',
        .renamed => 'R',
        .untracked => '?',
        .unmerged => 'U',
        .none => null,
    };
}

inline fn getGitStatusColor(files: zlist.Files, name: []const u8) Terminal.Color {
    const status = files.gitStatus(name) orelse return Terminal.Color.reset;
    return switch (status) {
        .modified => Terminal.Color.bright_yellow,
        .added => Terminal.Color.bright_green,
        .deleted => Terminal.Color.bright_red,
        .renamed => Terminal.Color.bright_blue,
        .untracked => Terminal.Color.bright_black,
        .unmerged => Terminal.Color.bright_red,
        .none => Terminal.Color.reset,
    };
}

test "detail size column expands to fit its widest value" {
    const io = std.testing.io;

    var tmp_dir = std.testing.tmpDir(.{});
    defer tmp_dir.cleanup();

    var wide_data: [1004]u8 = undefined;
    @memset(&wide_data, 0);
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "small.txt", .data = "123" });
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "wide.txt", .data = &wide_data });

    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    var files = try zlist.Files.init(
        arena.allocator(),
        io,
        tmp_dir.dir,
        .{ .show_detail = true },
    );
    defer files.deinit();

    var output_buf: [4096]u8 = undefined;
    var output_writer: std.Io.Writer = .fixed(&output_buf);
    const term = Terminal{ .writer = &output_writer, .mode = .no_color };

    const view_opt = LongViewOptions{
        .show_permissions = false,
        .show_user = false,
        .show_group = false,
        .show_time = false,
        .show_icon = false,
    };
    try listDetail(files, term, .{ .pure = true }, view_opt);

    try std.testing.expectEqualStrings(
        "   3B small.txt\n1004B wide.txt\n",
        output_writer.buffered(),
    );

    output_writer = .fixed(&output_buf);
    var header_view_opt = view_opt;
    header_view_opt.header = true;
    try listDetail(files, term, .{ .pure = true }, header_view_opt);

    try std.testing.expectEqualStrings(
        " Size Name\n   3B small.txt\n1004B wide.txt\n",
        output_writer.buffered(),
    );
}

test "recursive" {
    const io = std.testing.io;

    var tmp_dir = std.testing.tmpDir(.{});
    defer tmp_dir.cleanup();
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test1.txt", .data = "hello 1" });
    try tmp_dir.dir.writeFile(io, .{ .sub_path = "test2.txt", .data = "hello 2" });

    try tmp_dir.dir.createDirPath(io, "sub_dir");
    var tmp_sub_dir = try tmp_dir.dir.openDir(io, "sub_dir", .{ .iterate = true });
    defer tmp_sub_dir.close(io);
    _ = try tmp_sub_dir.createFile(io, "sub_test1.txt", .{});

    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();
    const allocator = arena.allocator();

    // stdout
    var stdout_buf: [4096]u8 = undefined;
    const stdout_file = std.Io.File.stdout();
    var stdout_writer = stdout_file.writer(io, &stdout_buf);

    var files = try zlist.Files.init(
        allocator,
        io,
        tmp_dir.dir,
        .{},
    );
    defer files.deinit();

    const term = try getTerminal(io, &stdout_writer.interface, stdout_file, true, true);

    try listRecursive(".", &files, term, "", true, tmp_dir.dir, .{ .pure = false }, .dot);
    try stdout_writer.interface.flush();
}
